# Copyright (c) 2020 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/lite/config/component/lite_component.gni")
import("//build/lite/config/subsystem/aafwk/config.gni")
import("//build/lite/config/test.gni")
import("//foundation/ability/ability_lite/ability_lite.gni")

lite_library("abilitymanager") {
  if (ohos_kernel_type == "liteos_m") {
    target_type = "static_library"

    sources = [
      "src/slite/ability_manager.cpp",
      "src/slite/ability_manager_client.cpp",
      "src/slite/ability_manager_inner.cpp",
      "src/slite/ability_record_state_data.cpp",
      "src/slite/abilityms_slite_client.cpp",
      "src/slite/mission_info.cpp",
    ]

    deps = [ "${hilog_lite_path}/frameworks/featured:hilog_static" ]

    include_dirs = [
      "${aafwk_lite_path}/interfaces/inner_api/abilitymgr_lite",
      "${aafwk_lite_path}/interfaces/inner_api/abilitymgr_lite/slite",
      "${aafwk_lite_path}/interfaces/kits/ability_lite",
      "${aafwk_lite_path}/interfaces/kits/ability_lite/slite",
      "${aafwk_lite_path}/interfaces/kits/want_lite",
      "${aafwk_lite_path}/services/abilitymgr_lite/include/util",
      "${appexecfwk_lite_path}/interfaces/kits/bundle_lite",
      "${appexecfwk_lite_path}/utils/bundle_lite",
      "${utils_lite_path}/include",
      "${utils_lite_path}/memory/include",
      "${ability_lite_samgr_lite_path}/interfaces/kits/registry",
      "${ability_lite_samgr_lite_path}/interfaces/kits/samgr",
      "//third_party/bounds_checking_function/include",
      "./include/slite",
    ]
  } else {
    target_type = "shared_library"

    sources = [
      "src/ability_callback_utils.cpp",
      "src/ability_manager.cpp",
      "src/ability_self_callback.cpp",
      "src/ability_service_manager.cpp",
      "src/abilityms_client.cpp",
    ]

    deps = [
      "${appexecfwk_lite_path}/frameworks/bundle_lite:bundle",
      "${communication_path}/ipc/interfaces/innerkits/c/ipc:ipc_single",
      "${hilog_lite_path}/frameworks/featured:hilog_shared",
    ]

    defines = [ "OHOS_APPEXECFWK_BMS_BUNDLEMANAGER" ]

    include_dirs = [
      "${aafwk_lite_path}/services/abilitymgr_lite/include",
      "${aafwk_lite_path}/services/abilitymgr_lite/include/util",
      "${aafwk_lite_path}/frameworks/abilitymgr_lite/include",
      "${aafwk_lite_path}/frameworks/want_lite/include",
      "${aafwk_lite_path}/interfaces/inner_api/abilitymgr_lite",
      "${aafwk_lite_path}/interfaces/kits/want_lite",
      "${aafwk_lite_path}/interfaces/kits/ability_lite",
      "${appexecfwk_lite_path}/utils/bundle_lite",
      "${appexecfwk_lite_path}/interfaces/kits/bundle_lite",
      "${appexecfwk_lite_path}/frameworks/bundle_lite/include",
      "${utils_lite_path}/include",
      "${utils_lite_path}/memory/include",
      "${communication_path}/ipc/interfaces/innerkits/c/ipc/include",
      "${kv_store_path}/interfaces/inner_api/kv_store/include",
      "${ability_lite_samgr_lite_path}/interfaces/kits/registry",
      "${ability_lite_samgr_lite_path}/interfaces/kits/samgr",
      "//third_party/bounds_checking_function/include",
      "./include",
      "./",
    ]
  }

  if (ohos_kernel_type == "liteos_m") {
    include_dirs += [ "${hilog_lite_path}/interfaces/native/kits/hilog_lite" ]
  }
}

unittest("ability_manager_inner_test") {
  output_extension = "bin"
  output_dir = "$root_out_dir/test/unittest/ManagerInnerTest_lv0"

  sources = [ "${ability_lite_path}/frameworks/abilitymgr_lite/unittest/ability_manager_inner_test.cpp" ]
}

lite_component("aafwk_abilityManager_lite") {
  features = [ ":abilitymanager" ]
}
